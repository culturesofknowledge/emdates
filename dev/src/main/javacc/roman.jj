options {
  IGNORE_CASE = true;
}
PARSER_BEGIN(RomanDateParser)
package nl.knaw.huygens.lobsang.core.parsers;

import java.io.ByteArrayInputStream;
import java.io.InputStream;
import java.time.Month;
import java.time.MonthDay;

import nl.knaw.huygens.lobsang.api.YearMonthDay;

public class RomanDateParser {
  private static final RomanDateTable table = new RomanDateTable();

  private static RomanDateParser parser = null;

  public static YearMonthDay parse(String input) throws ParseException {
    final InputStream is = new ByteArrayInputStream(input.getBytes());

    // handle JavaCC's instantiation / reinitialisation contract
    if (parser == null) {
      parser = new RomanDateParser(is);
    }
    else {
      ReInit(is);
    }

    return parser.parseDate();
  }

  // Thanks! https://stackoverflow.com/a/29218799
  private static int evaluateRomanNumerals(String roman) {
      return (int) evaluateNextRomanNumeral(roman.toUpperCase(), roman.length() - 1, 0);
  }

  private static double evaluateNextRomanNumeral(String roman, int pos, double rightNumeral) {
      if (pos < 0) return 0;
      char ch = roman.charAt(pos);
      double value = Math.floor(Math.pow(10, "IXCM".indexOf(ch))) + 5 * Math.floor(Math.pow(10, "VLD".indexOf(ch)));
      return value * Math.signum(value + 0.5 - rightNumeral) + evaluateNextRomanNumeral(roman, pos - 1, value);
  }

  private static MonthDay lookup(String normalizedRomanDate) throws ParseException {
    final MonthDay md = table.lookup(normalizedRomanDate);

    if (md == null) {
      throw new ParseException("Unrecognised Roman date: \"" + normalizedRomanDate + "\"");
    }

    return md;
  }

  private static YearMonthDay inYear(Integer year, MonthDay md) {
    return new YearMonthDay(year == null ? -1 : year, md.getMonthValue(), md.getDayOfMonth());
  }

}
PARSER_END(RomanDateParser)

SKIP :
{
  " "
| "\n"
| "\r"
| "\t"
}

// Gleaned from https://github.com/antlr/grammars-v4/blob/master/romannumerals/romannumerals.g4
TOKEN :
{
  <#ONES_REP  : "i" | "ii" | "iii" >
| <#ONES      : <ONES_REP> | "iv" | "v" (<ONES_REP>)? | "ix" >
| <#TENS_REP  : "x" | "xx" | "xxx" >
| <#TENS_PART : <TENS_REP> | "xl" | "l" (<TENS_REP>)? | "xc" >
| <#TENS      : <TENS_PART> <ONES> | <TENS_PART> | <ONES> >
| <#HUNS_REP  : "c" | "cc" | "ccc" >
| <#HUNS_PART : <HUNS_REP> | "cd" | "d" (<HUNS_REP>)? | "cm" >
| <#HUNS      : <HUNS_PART> <TENS> | <HUNS_PART> | <TENS> >
| <#THOUS_PART: ("m")+ >
| <#THOUSANDS : <THOUS_PART> <HUNS> | <THOUS_PART> | <HUNS> >
| <Roman      : <THOUSANDS> >
}

TOKEN :
{
  < Arabic: (["0"-"9"])+ >
}

TOKEN :
{
  <#Ius  : "ius">
| <#Ember: (["b","e","m","r","."])+ >
}

// "Comitis" in dates usually means "sent from". It oftentimes is preceded by the placename where a letter was sent
// from. E.g., "Hagae comitis" meaning "sent from The Hague". It is not a part of the date itself, so we can ignore
// it. By recognising it in the parser, we have the option to flag it to the scholar, rather than just saying "parse
// error".
TOKEN :
{
  <Comitis: "comitis">
}

TOKEN :
{
  < Jan: ("ian" | "jan") (["a","i","r","s","u","."])* >
| < Feb: ("feb") (".")? >
| < Mar: ("mar" (["t"] (["i"])? (["i"])?)? (".")?) | "m." >
| < Apr: "apr" (["i","l"])* (".")? >
| < May: "ma" (["i","j"])* (".")? >
| < Jun: ("iun"|"ivn") ("." | <Ius>)? >
| < Jul: ("iul"|"ivl") ("." | <Ius>)? >
| < Aug: ("aug"|"avg") (["i","s","t","u"])* (".")? >
| < Sep: "sep" (["b","e","m","r","t"])* (".")? >
| < Oct: ("oct"|"okt") (["o","b","e","r"])* (".")? >
| < Nov: ("nov"|"nou") (["b","e","m","r"])* (".")? >
| < Dec: "dec" (<Ember>)? >
}

TOKEN :
{
  < Ante: ("ante" | ("a" ".")) >
| < Bis:  "bis" >
| < Diem: ("diem" | ("d" ".")) >
| < Ides   : ("e")? "id" ("es" | ".") >
| < Kalends: (["c", "k"]) "al" ("ends" | ".")? >
| < Nones  : "non" (["a","e","i"] "s")? (".")? >
| < Pridie: ("pr") (["d","e","i"])* (".")? >
}

// If all of the above fails, it may just be (superfluous) text.
TOKEN :
{
  <Text: (["a"-"z"])+ >
}

YearMonthDay parseDate() :
{
  final String roman;
  String event = null;
  String month = null;
  String msg = null;
  Token bis = null;
  Integer day = null;
  Integer year = null;
}
{
  [msg=superfluous()]
  {
      if (msg != null) {
        // Print for now...maybe throw ParseException?
        System.err.println(msg);
      }
  }
  (
    // e.g., "<Month>" as "Kal. <Month>" (which in turn means "1st of <Month>".
    month=month() [year=number()] <EOF>
    {
      final MonthDay md = lookup("KAL. " + month);
      return inYear(year, md);
    }
  |
    // e.g., "Kal. Mart.", "Id. Feb".
    event = event() month=month() [year=number()] <EOF>
    {
      final MonthDay md = lookup(event + " " + month);
      return inYear(year, md);
    }
  |
    // e.g., "A.D. VII Ides Mart."
    [<Ante> <Diem>] [bis=<Bis>] roman=roman() [event=event()] month=month() [year=number()] <EOF>
    {
      if (event == null) {
        final MonthDay md = lookup("KAL. " + month);
        return inYear(year, md.withDayOfMonth(evaluateRomanNumerals(roman)));
      }

      if (bis == null) {
        final MonthDay md = lookup("A.D. " + roman + " " + event + " " + month);
        return inYear(year, md);
      }

      if (month.equals("MART.") && "VI".equals(roman) && "KAL.".equals(event)) {
        final MonthDay md = lookup("A.D. BIS VI KAL. M.");
        return inYear(year, md);
      }

      throw new ParseException("BIS only defined on A.D. BIS VI KALENDS MARTIUS!");
    }
  |
    // e.g., "Prid. Kal. Mart.", "Prid. Non. Apr."
    <Pridie> event=event() month=month() year=number() <EOF>
    {
      final MonthDay md = table.lookup("PRID. " + event + " " + month);
      return inYear(year, md);
    }
  )
}

String event() :
{}
{
  <Kalends> { return "KAL."; }
| <Ides>    { return "ID."; }
| <Nones>   { return "NON."; }
}

String month() :
{}
{
  <Jan> { return "IAN."; }
| <Feb> { return "FEB."; }
| <Mar> { return "MART."; }
| <Apr> { return "APR."; }
| <May> { return "MAI."; }
| <Jun> { return "IVN."; }
| <Jul> { return "IVL."; }
| <Aug> { return "AVG."; }
| <Sep> { return "SEPT."; }
| <Oct> { return "OKT."; }
| <Nov> { return "NOV."; }
| <Dec> { return "DEC."; }
}

String roman() :
{
  Token token;
}
{
  token=<Roman> ["."] { return token.image; }
}

String arabic() :
{
  Token token;
}
{
  token=<Arabic> ["."] { return token.image; }
}

int number() :
{
  String str;
}
{
  str=arabic() { return Integer.valueOf(str); }
| str=roman()  { return evaluateRomanNumerals(str); }
}

String superfluous() :
{
  Token text = null;
}
{
  [text=<Text>] <Comitis> {
    String hint = "Comitis is not part of a date. It means \"sent from\", and is usually preceded by a place";
    if (text != null) hint += ", \"" + text.image + "\" in this case?";
    else hint += ". Please remove.";
    return hint;
  }
}