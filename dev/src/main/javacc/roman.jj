options {
  IGNORE_CASE = true;
}
PARSER_BEGIN(RomanDateParser)
package nl.knaw.huygens.lobsang.core.parsers;

import java.io.ByteArrayInputStream;
import java.io.InputStream;
import java.time.Month;
import java.time.MonthDay;
import java.util.HashSet;
import java.util.Optional;
import java.util.Set;

import nl.knaw.huygens.lobsang.api.YearMonthDay;

public class RomanDateParser {
  public static final String ILLEGAL_ROMAN_NUMERAL = "Illegal Roman numeral: ";
  public static final String INVALID_ROMAN_COUNT = "Invalid Roman count ";

  public static final String DEFAULT_COUNT = "III"; // lowest roman used in "ante diem" notations

  public static final int DEFAULT_YEAR_IF_UNPARSABLE = 0;

  private static final RomanDateTable table = new RomanDateTable();

  private static RomanDateParser parser = null;

  public static YearMonthDay parse(String input) throws ParseException {
    final InputStream is = new ByteArrayInputStream(input.getBytes());

    // handle JavaCC's instantiation / reinitialisation contract
    if (parser == null) {
      parser = new RomanDateParser(is);
    }
    else {
      ReInit(is);
    }

    return parser.parseDate();
  }

  // Thanks! https://stackoverflow.com/a/29218799
  private static int evaluateRomanNumerals(String roman) {
      final var normalized = roman.toUpperCase().replaceAll("[\\s]", "");
      final var i = (int) evaluateNextRomanNumeral(normalized, normalized.length() - 1, 0);

      // ad hoc 'repair' for creative use of 'subtract two from next number' in, e.g., 'anno M D IIC.'
      if (normalized.endsWith("IIC") || normalized.endsWith("IIX")) {
        return i - 2;
      }

      return i;
  }

  private static double evaluateNextRomanNumeral(String roman, int pos, double rightNumeral) {
      if (pos < 0) return 0;
      char ch = roman.charAt(pos);
      double value = Math.floor(Math.pow(10, "IXCM".indexOf(ch))) + 5 * Math.floor(Math.pow(10, "VLD".indexOf(ch)));
      return value * Math.signum(value + 0.5 - rightNumeral) + evaluateNextRomanNumeral(roman, pos - 1, value);
  }

  private static MonthDay lookup(String normalizedRomanDate) throws ParseException {
    final MonthDay md = table.lookup(normalizedRomanDate);

    if (md == null) {
      throw new ParseException("Unrecognised Roman date: \"" + normalizedRomanDate + "\"");
    }

    return md;
  }

  private static YearMonthDay inYear(Optional<Integer> year, MonthDay md) {
    final YearMonthDay result = new YearMonthDay(year.orElse(0), md.getMonthValue(), md.getDayOfMonth());

    if (year.isEmpty()) {
      result.setYearSpecified(false);
      result.addNote("Missing year indication");
    }

    return result;
  }

  private static YearMonthDay inYear(Integer year, MonthDay md) {
    return inYear(Optional.ofNullable(year), md);
  }

  private static String asRomanForEvent(int num) {
    switch(num){
      case 3: return "III";
      case 4: return "IV";
      case 5: return "V";
      case 6: return "VI";
      case 7: return "VII";
      case 8: return "VIII";
      case 9: return "IX";
      case 10: return "X";
      case 11: return "XI";
      case 12: return "XII";
      case 13: return "XIII";
      case 14: return "XIV";
      case 15: return "XV";
      case 16: return "XVI";
      case 17: return "XVII";
      case 18: return "XVIII";
      default:
        return String.valueOf(num);
    }
  }

}
PARSER_END(RomanDateParser)

SKIP :
{
  " "
| "\n"
| "\r"
| "\t"
}

TOKEN :
{
  < Arabic: (["0"-"9"])+ >
}

// Gleaned from https://github.com/antlr/grammars-v4/blob/master/romannumerals/romannumerals.g4
// Optional spaces allowed in #THOUSANDS and #HUNS to account for dates written in "M D LXVV" form
TOKEN :
{
  <#ONES_REP  : "i" | "ii" | "iii" >
| <#FOUR      : "iv" | "iiii" > // allow common variant of 'iiii' for 'iv'
| <#ONES      : <ONES_REP> | <FOUR> | "v" (<ONES_REP>)? | "ix" | "iix" >
| <#TENS_REP  : "x" | "xx" | "xxx" >
| <#TENS_PART : <TENS_REP> | "xl" | "l" (<TENS_REP>)? | "xc" | "ic" | "iic" >
| <#TENS      : <TENS_PART> <ONES> | <TENS_PART> | <ONES> >
| <#HUNS_REP  : "c" | "cc" | "ccc" >
| <#HUNS_PART : <HUNS_REP> | "cd" | "d" ([" "])? (<HUNS_REP>)? | "cm" >
| <#HUNS      : <HUNS_PART> ([" "])? <TENS> | <HUNS_PART> | <TENS> >
| <#THOUS_PART: ("m")+ >
| <#THOUSANDS : <THOUS_PART> ([" "])? <HUNS> | <THOUS_PART> | <HUNS> >
| <Roman      : <THOUSANDS> >
}

TOKEN :
{
  <PseudoRoman: (["c","d","i","l","m","x"])+ >
}

// "Comitis" in dates usually means "sent from". It is often preceded by the placename where a letter was sent
// from. E.g., "Hagae comitis" meaning "sent from The Hague". It is not a part of the date itself, so we can ignore
// it. By recognising it in the parser, we have the option to flag it to the scholar, rather than just saying "parse
// error".
TOKEN :
{
  <Comitis: "comitis">
}

TOKEN :
{
  <Quin: "q" ("u"|"v") "i" ("n")? ("c")? (["b","e","i","l","s","t","u"])* (".")? >
| <Sext: "sex" (["e","i","l","s","t","u"])* (".")? >
| <Octem: "iix" ([".","b","i","m","r","u"])+ >
| <Novem: "ix" ([".","b","i","m","r","u"])+ >
| <Decem: "x" (".")? (["b","i","m","r","u"])+ (".")? >
}

TOKEN :
{
  < Jan: ("ian" | "jan") (["a","i","r","s","u"])* (".")? >
| < Feb: "feb" (["a","i","j","r","s","u"])* (".")? >
| < Mar: ("mar" (["a","i","s","t","u"])* (".")?) | "m." >
| < Apr: "apr" (["b","e","i","l","s","u"])* (".")? >
| < May: "ma" (["a","i","j","s"])* (".")? >
| < Jun: (["i","j"]) (["u","v"]) "n" (["a","i","u","s"])* (".")? >
| < Jul: ("iul"|"ivl") (["a","c","i","o","u","s"])* (".")? >
| < Aug: ("aug"|"avg") (["a","i","s","t","u"])* (".")? >
| < Sep: "sep" (["b","e","m","i","r","s","t"])* (".")? >
| < Oct: ("oct"|"okt") (["o","b","e","i","r","s","u"])* (".")? >
| < Nov: ("nov"|"nou") (["b","e","i","m","r","s"])* (".")? >
| < Dec: "dec" (["b","e","i","m","r","s"])* (".")? >
}

TOKEN :
{
  < A_dot: "a." >
| < D_dot: "d." >
| < Anno: ("ann" ["i","o"] (".")?)>
| < Ante: "ante" >
| < Bis:  "bis" (".")? >
| < Christ: "christi" >
| < Die: "die" >
| < Diem: "diem" >
| < Ides   : ("e")? "id" ("ib")? ("es"|"us")? (".")? >
| < Kalends: ("k." | (["c", "k"]) "al" (["e","i","n","d","s"])* ("arum")? (".")? ) >
| < Nones  : "non" (["a","e","i"] "s" | "arum")? (".")? >
| < Pridie: "p" ("rop" | "ost")? "r" (["d","e","i"])* (".")? >
}

TOKEN :
{
  < Julian: (["i","j"]) (["u","v"]) "lian" (["a","i","s"])* (".")? >
| < Gregorian: "gregorian" (["a","i","s"])* (".")? >
| < Stilo: "st" (["i","y"]) "lo" >
| < Novo: "novo" (".")? >
}

// If all of the above fails, it may just be (superfluous) text.
TOKEN :
{
  <Text: (["a"-"z","A"-"Z"])+ >
}

YearMonthDay parseDate() :
{
  final Set<String> notes = new HashSet<String>();
  final YearMonthDay result;
  String arabic = null;
  String roman = null;
  Integer day = null;
  Integer year = null;
  String event = null;
  String month = null;
  String msg = null;
  Token bis = null;
  Token t1 = null;
  Token t2 = null;
}
{
  [msg=superfluous()]
  {
      if (msg != null) {
        notes.add(msg);
      }
  }
  (
    LOOKAHEAD(3) // needed to distinguish between "IUL." as a month and "IUL." as Julian calendar indication
    month=month() [year=year(notes)] // e.g., "<Month>" as "Kal. <Month>" (which in turn means "1st of <Month>").
    {
      result = inYear(year, lookup("KAL. " + month));
      notes.add("Missing day of month indication, assuming Kalends (first day of the month).");
    }
  |
    // e.g., "Kal. Mart.", "Id. Feb".
    event = event() month=month() [[","] year=year(notes)]
    {
      final MonthDay md = lookup(event + " " + month);
      result = inYear(year, md);
    }
  |
    // e.g., "A.D. VII Ides Mart."
    LOOKAHEAD(3)
    [<Die>] [(<Ante>|<A_dot>) (<Diem>|<D_dot>)] [bis=<Bis>] (roman=roman(notes) | arabic=arabic() | t1=<PseudoRoman>) [","] [event=event()] month=month() [[","] year=year(notes) | t2=<PseudoRoman>]
    {
      if (t1 != null) {
        notes.add(ILLEGAL_ROMAN_NUMERAL + t1.image + ", winging it using count=" + DEFAULT_COUNT);
        roman = DEFAULT_COUNT;
      }
      else if (event != null) {
        int num = (roman == null ? Integer.parseInt(arabic) : evaluateRomanNumerals(roman));
        if (num < 3 || num > 18) {
          notes.add(INVALID_ROMAN_COUNT + roman + " in 'ante diem <count> <event>' notation, must be between iii and xviii (inclusive)");
          roman = DEFAULT_COUNT;
        }
      }

      if (t2 != null) {
        notes.add(ILLEGAL_ROMAN_NUMERAL + t2.image + ", winging it using year=" + DEFAULT_YEAR_IF_UNPARSABLE);
        year = DEFAULT_YEAR_IF_UNPARSABLE;
      }

      if (event == null) {
        final MonthDay md = lookup("KAL. " + month);
        final int dayOfMonth = (roman == null ? Integer.parseInt(arabic) : evaluateRomanNumerals(roman));
        result = inYear(year, md.withDayOfMonth(dayOfMonth));
      }
      else if (bis == null) {
        final int daysBeforeEvent;
        if (roman == null) {
          daysBeforeEvent = Integer.parseInt(arabic);
        } else {
          daysBeforeEvent = evaluateRomanNumerals(roman);
        }
        final String daysBeforeEventInRoman = asRomanForEvent(daysBeforeEvent);
        final MonthDay md = lookup("A.D. " + daysBeforeEventInRoman + " " + event + " " + month);
        result = inYear(year, md);
      }
      else if (month.equals("MART.") && "VI".equals(roman) && "KAL.".equals(event)) {
        final MonthDay md = lookup("A.D. BIS VI KAL. M.");
        result = inYear(year, md);
      }
      else {
        throw new ParseException("BIS only defined on A.D. BIS VI KALENDS MARTIUS!");
      }
    }
  |
    // e.g., "Prid. Kal. Mart.", "Prid. Non. Apr."
    t1=<Pridie> event=event() month=month() [[","] year=year(notes)]
    {
      MonthDay md = table.lookup("PRID. " + event + " " + month);
      if (t1.image.startsWith("pro")) {
        notes.add("Creative use of pridie (i.c. '" + t1.image + "') starts with 'pro': interpreting date to be the day before pridie");
        md = md.withDayOfMonth(md.getDayOfMonth() - 1);
      } else if (t1.image.startsWith("post")) {
        notes.add("Creative use of pridie (i.c. '" + t1.image + "') starts with 'post': interpreting date to be the day after the event that pridie preceded");
        md = table.lookup(event + " " + month);
        md = md.withDayOfMonth(md.getDayOfMonth() + 1);
      }
      result = inYear(year, md);
    }
  |
    // e.g., "anno MDXCI" just a year with no further specification
    [calendar(notes)] (<Anno> | <A_dot> <D_dot>) (<Christ>)? year=number(notes)
    {
      result = inYear(year, lookup("KAL. IAN.")); // hack to use 1st of Jan to create valid YMD
      notes.add("Missing day and month details, assuming 1 Jan.");
    }
  )
  <EOF> {
    if (!notes.isEmpty()) {
      result.addNotes(notes);
    }
    return result;
  }
}

String event() :
{}
{
  <Kalends> { return "KAL."; }
| <Ides>    { return "ID."; }
| <Nones>   { return "NON."; }
}

String month() :
{}
{
  <Jan> { return "IAN."; }
| <Feb> { return "FEB."; }
| <Mar> { return "MART."; }
| <Apr> { return "APR."; }
| <May> { return "MAI."; }
| <Jun> { return "IVN."; }
| (<Jul> | <Quin>) { return "IVL."; }
| (<Aug> | <Sext>) { return "AVG."; }
| <Sep> { return "SEPT."; }
| (<Oct> | <Octem>) { return "OCT."; }
| (<Nov> | <Novem>) { return "NOV."; }
| (<Dec> | <Decem>) { return "DEC."; }
}

Integer year(Set<String> notes) :
{
  Integer year;
}
{
  year=number(notes) { return year; }
| (<Anno> [<Christ>] | <A_dot> <D_dot>) year=number(notes) { return year; }
| LOOKAHEAD(3) calendar(notes) [","] [<Anno> [<Christ>]] year=number(notes) { return year; }
| calendar(notes) { return null; }
}

void calendar(Set<String> notes) :
{}
{
  (<Jul> | <Julian>) { notes.add("In Julian calendar"); }
| <Gregorian> { notes.add("In Gregorian calendar"); }
| <Stilo> <Novo>  { notes.add("In New Style calendar"); }
}

String roman(Set<String> notes) :
{
  Token token;
}
{
  token=<Roman> ["."] {
    var count = token.image.toUpperCase();
    if (count.contains(" ")) {
      notes.add("Detected whitespace in roman numeral: '" + count + "'");
      count = count.replaceAll("[\\s]", "");
    }

    if (count.contains("IIII")) {
      notes.add("Parsing 'IIII' as variant of 'IV' in: '" + count + "'");
      count = count.replaceAll("IIII", "IV");
    }

    if (count.endsWith("IIC")) {
      notes.add("Parsing 'IIC' as 'XCVIII' (98) in: '" + count + "'");
    }

    if (count.endsWith("IIX")) {
      notes.add("Parsing 'IIX' as 'XVIII' (8) in: '" + count + "'");
    }

    return count;
  }
}

String arabic() :
{
  Token token;
}
{
  token=<Arabic> ["."] { return token.image; }
}

int number(Set<String> notes) :
{
  String str;
}
{
  str=arabic() { return Integer.valueOf(str); }
| str=roman(notes)  { return evaluateRomanNumerals(str); }
}

String superfluous() :
{
  Token text = null;
}
{
  [text=<Text>] <Comitis> {
    String hint = "Comitis is not part of a date. It means \"sent from\", and is usually preceded by a place";
    if (text != null) hint += ", \"" + text.image + "\" in this case?";
    else hint += ". Please remove.";
    return hint;
  }
}
